plugins {
  //id "com.moowork.node" version "1.3.1"
  id("com.github.node-gradle.node") version "2.2.0"
}

node {
  // Version of node to use.
  version = '12.13.1'

  // Version of npm to use.
  npmVersion = '6.12.1'

  // Version of Yarn to use.
  yarnVersion = '1.19.2'

  // Base URL for fetching node distributions (change if you have a mirror).
  distBaseUrl = 'https://nodejs.org/dist'

  // If true, it will download node using above parameters.
  // If false, it will try to use globally installed node.
  download = true

  // Set the work directory for unpacking node
  workDir = file("${project.buildDir}/nodejs")

  // Set the work directory for NPM
  npmWorkDir = file("${project.buildDir}/npm")

  // Set the work directory for Yarn
  yarnWorkDir = file("${project.buildDir}/yarn")

  // Set the work directory where node_modules should be located
  nodeModulesDir = file("${project.buildDir}")
}

task create_workdir(type: Copy) {
  // copy .. ..
  from('src/main/webapp')
  into project.buildDir.path
}
//default yarn_install
task package_install(type: YarnTask, dependsOn: create_workdir) {
  // add the express package only
  args = ['install']   //'--modules-folder', project.buildDir.path + '/node_modules'
}
task build_package(type: YarnTask, dependsOn: package_install) {
  //只安装快递包
  args = ['run', 'build']
}
task webapp(type: Copy, dependsOn: build_package) {
  from("${project.buildDir}/build")
  into project(':sylph-dist').buildDir.path + '/webapp'
}
//assemble.dependsOn webapp
project(':sylph-dist').dist.dependsOn webapp

dependencies {
  annotationProcessor 'org.projectlombok:lombok:1.18.8'
  compileOnly 'org.projectlombok:lombok:1.18.8'

  api group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
  api(project(':sylph-spi'))

  api group: 'org.eclipse.jetty', name: 'jetty-server', version: deps.jetty
  api group: 'org.eclipse.jetty', name: 'jetty-webapp', version: deps.jetty
  api group: 'org.eclipse.jetty', name: 'jetty-servlets', version: deps.jetty

  api group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'

  api("org.glassfish.jersey.containers:jersey-container-servlet:$deps.jersey") {
    exclude(module: 'javassist')
  }
  api("org.glassfish.jersey.media:jersey-media-json-jackson:$deps.jersey") {
    exclude(group: 'com.fasterxml.jackson.core')
    exclude(module: 'javassist')
    exclude(module: 'jackson-jaxrs-base')
    exclude(module: 'jackson-jaxrs-json-provider')
    exclude(module: 'jackson-module-jaxb-annotations')
  }
  api(group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: deps.jersey) {
    exclude(module: 'javassist')
  }
  api("org.glassfish.jersey.inject:jersey-hk2:$deps.jersey") {
    exclude(module: 'javassist')
  }

  api group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: deps.jackson
}