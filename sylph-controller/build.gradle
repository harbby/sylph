plugins {
  id "com.moowork.node" version "1.2.0"
}

node {
  // Version of node to use.
  version = '10.16.0'

  // Version of npm to use.
  npmVersion = '6.9.0'

  // Version of Yarn to use.
  yarnVersion = '1.9.4'

  // Base URL for fetching node distributions (change if you have a mirror).
  distBaseUrl = 'https://nodejs.org/dist'

  // If true, it will download node using above parameters.
  // If false, it will try to use globally installed node.
  download = true

  // Set the work directory for unpacking node
  workDir = file("${project.buildDir}/nodejs")

  // Set the work directory for NPM
  npmWorkDir = file("${project.buildDir}/npm")

  // Set the work directory for Yarn
  yarnWorkDir = file("${project.buildDir}/yarn")

  // Set the work directory where node_modules should be located
  nodeModulesDir = file("${project.projectDir}/src/main/webapp")
}

//default yarn_install
task package_install(type: YarnTask) {
  // add the express package only
  args = ['install']   //'--modules-folder', project.buildDir.path + '/node_modules'
}
task build_package(type: YarnTask, dependsOn: package_install) {
  //只安装快递包
  args = ['run', 'build']
}
task build_webapp(type: Copy, dependsOn: build_package) {
  from('src/main/webapp/build')
  into project(':sylph-dist').buildDir.path + '/webapp'
}
assemble.dependsOn 'build_webapp'
dependencies {
  annotationProcessor 'org.projectlombok:lombok:1.18.8'
  compileOnly 'org.projectlombok:lombok:1.18.8'

  compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
  compile(project(':sylph-spi'))

  compile group: 'org.eclipse.jetty', name: 'jetty-server', version: deps.jetty
  compile group: 'org.eclipse.jetty', name: 'jetty-webapp', version: deps.jetty
  compile group: 'org.eclipse.jetty', name: 'jetty-servlets', version: deps.jetty

  compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'

  compile("org.glassfish.jersey.containers:jersey-container-servlet:$deps.jersey") {
    exclude(module: 'javassist')
  }
  compile("org.glassfish.jersey.media:jersey-media-json-jackson:$deps.jersey") {
    exclude(group: 'com.fasterxml.jackson.core')
    exclude(module: 'javassist')
    exclude(module: 'jackson-jaxrs-base')
    exclude(module: 'jackson-jaxrs-json-provider')
    exclude(module: 'jackson-module-jaxb-annotations')
  }
  compile(group: 'org.glassfish.jersey.media', name: 'jersey-media-multipart', version: deps.jersey) {
    exclude(module: 'javassist')
  }
  compile("org.glassfish.jersey.inject:jersey-hk2:$deps.jersey") {
    exclude(module: 'javassist')
  }

  compile group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: deps.jackson
}